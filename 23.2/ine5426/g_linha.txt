PROGRAM -> STATEMENT | FUNCLIST | &

FUNCLIST -> FUNCDEF FUNCLISTAUX

FUNCLISTAUX -> FUNCLIST | &

FUNCDEF -> def ident(PARAMLIST){STATELIST}

PARAMLIST -> DATATYPE ident PARAMLISTAUX | &

DATATYPE -> int | float | string

PARAMLISTAUX -> , PARAMLIST | &

STATEMENT -> (VARDECL; | ATRIBSTAT; | PRINTSTAT; | READSTAT; | RETURNSTAT; | IFSTAT | FORSTAT | {STATELIST} | break; | ;)

VARDECL -> DATATYPE ident OPTVECTOR

OPTVECTOR -> [int_constant] OPTVECTOR | &

ATRIBSTAT -> LVALUE = ATRIBSTATRIGHT

ATRIBSTATRIGHT -> FUNCCALL | ALLOCEXPRESSION

FUNCCALL -> ident(PARAMLISTCALL)

PARAMLISTCALL -> ident PARAMLISTCALLAUX | &

PARAMLISTCALLAUX -> , PARAMLISTCALL | &

PRINTSTAT -> print EXPRESSION

READSTAT -> read LVALUE

RETURNSTAT -> return

IFSTAT -> if(EXPRESSION) STATEMENT ELSESTAT

ELSESTAT -> else STATEMENT | &

FORSTAT -> for(ATRIBSTAT; EXPRESSION; ATRIBSTAT) STATEMENT

STATELIST -> STATEMENT OPT_STATELIST

OPT_STATELIST -> STATELIST | &

ALLOCEXPRESSION -> new DATATYPE[NUMEXPRESSION] OPT_ALLOC_NUMEXP

OPT_ALLOC_NUMEXP -> [NUMEXPRESSION] OPT_ALLOC_NUMEXP | &

EXPRESSION -> NUMEXPRESSION OPT_REL_OP_NUM_EXPR

OPT_REL_OP_NUM_EXPR -> REL_OP NUMEXPRESSION | &

REL_OP -> < | > | <= | >= | == | /=

NUMEXPRESSION -> TERM REC_PLUS_MINUS_TERM

REC_PLUS_MINUS_TERM -> PLUS_OR_MINUS TERM REC_PLUS_MINUS_TERM | &

PLUS_OR_MINUS -> + | -

TERM -> UNARYEXPR REC_UNARYEXPR

REC_UNARYEXPR -> UNARYEXPR_OP TERM | &

UNARYEXPR_OP -> * | / | %

UNARYEXPR -> PLUS_OR_MINUS FACTOR | FACTOR

FACTOR -> int_constant | float_constant | string_constant | null | LVALUE |(NUMEXPRESSION)

LVALUE -> ident OPT_ALLOC_NUMEXP